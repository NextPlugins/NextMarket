plugins {
    id "java"

    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version "1.3.1"

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    implementation 'com.github.Yuhtin:update-checker:1.2'
    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'de.tr7zw:item-nbt-api:2.9.2'

    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    implementation "com.google.inject:guice:5.0.1"

    def lombok = "org.projectlombok:lombok:1.18.16"

    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.nextmarket.NextMarket'
    authors = ['NextPlugins']
    website = 'https://nextplugins.com.br'
    version = "${project.version}"
    apiVersion = "1.13"
    depend = ['Vault']
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.yuhtin.updatechecker', 'com.nextplugins.nextmarket.libs.updatechecker'
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.nextmarket.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.nextmarket.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.nextmarket.libs.hikari'

    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.nextmarket.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.nextmarket.libs.command.bukkit'

    relocate 'com.google.common', 'com.nextplugins.nextmarket.libs.guice.common'
    relocate 'com.google.inject', 'com.nextplugins.nextmarket.libs.guice.inject'
    relocate 'com.google.thirdparty', 'com.nextplugins.nextmarket.libs.guice.thirdparty'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.nextmarket.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.nextmarket.libs.annotations'
}

compileJava {
    options.encoding = "UTF-8"

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
